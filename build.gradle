plugins {
    id 'org.jetbrains.intellij' version '1.3.0'
    id 'org.jetbrains.changelog' version '1.3.1'
    id 'org.jetbrains.kotlin.jvm' version '1.6.10'
    id 'org.jlleitschuh.gradle.ktlint' version '10.3.0'
}

group 'info.voidev.lspidea'
version '0.1.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.14.0'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    kotlinDaemonJvmArgs = ['--add-opens=java.base/java.lang=ALL-UNNAMED', '--add-opens=java.base/java.io=ALL-UNNAMED']
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = 17
        freeCompilerArgs += '-Xjvm-default=enable'
    }
}

changelog {
    header = "${-> version.get()}"
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version = '2021.3.1'

    plugins = ['org.intellij.plugins.markdown'] // 213.6461.19
}

patchPluginXml {
    changeNotes = provider { changelog.getLatest().toHTML() }
}

test {
    useJUnit()
}

ktlint {
    outputToConsole = true
    filter {
        // exclude receives relative paths, meaning they don't contain '/test/'
        // see e.g. https://github.com/JLLeitschuh/ktlint-gradle/issues/222

        // Exclude test folder because formatting in tests is usually finicky
        exclude { projectDir.toURI().relativize(it.file.toURI()).path.contains('/test/') }
    }
}
